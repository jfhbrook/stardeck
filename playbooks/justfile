set dotenv-load := true

default:
  @just --list

# Set everything up
setup:
  @just install

# Format playbooks
format:
  yamlfmt *.yml ./group_vars/*.yml ./host_vars/*.yml ./inventory/*.yml

# Lint playbooks and scripts
lint:
  ../scripts/with-all-playbooks.sh just playbook inventory --syntax-check
  ../scripts/with-all-playbooks.sh just ansible-lint
  shellcheck scripts/*.sh

# Run ansible-lint
ansible-lint *argv:
  ./scripts/with-environment.sh ansible-lint {{ argv }}

# Run a playbook
playbook playbook *argv:
  ./scripts/with-environment.sh ansible-playbook -i inventory.yml --ask-become-pass '{{ playbook }}' {{ argv }}

# Check a playbook
check inventory *argv:
  @just playbook '{{ inventory }}' --check {{ argv }}

# Install ansible roles and collections
install *argv:
  ./scripts/with-environment.sh ansible-galaxy install -r ../requirements.yml {{ argv }}

# Run ansible-config
config action *argv:
  ./scripts/with-environment.sh ansible-config {{ action }} {{ argv }}

# Dump ansible facts for a target
facts target:
  ./scripts/with-environment.sh ansible -i inventory '{{ target }}' -m ansible.builtin.setup
